plugins{
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
}

version '1.0'

sourceSets.main.java.srcDirs = ["lib/src"]

repositories{
    mavenCentral()
    maven{ url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
    maven{ url 'https://www.jitpack.io' }
}

java{
    targetCompatibility = 8
    sourceCompatibility = JavaVersion.VERSION_17
}

ext{
    //the build number that this mod is made for
    mindustryVersion = 'v151.1'
    jabelVersion = "93fde537c7"
    //windows sucks
    isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
    sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

//java 8 backwards compatibility flag
allprojects{
    tasks.withType(JavaCompile){
        options.compilerArgs.addAll(['--release', '8'])
    }
}

spotless{
    java{
        removeUnusedImports()
        importOrder()
        target 'src/**/*.java'
    }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"

    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
}

//force arc version
configurations.all{
    resolutionStrategy.eachDependency { details ->
        if(details.requested.group == 'com.github.Anuken.Arc'){
            details.useVersion "$mindustryVersion"
        }
    }
}

task jarAndroid{
    dependsOn "jar"

    doLast{
        if(!sdkRoot || !new File(sdkRoot).exists()) {
            logger.warn("Android SDK not found, skipping Android build")
            return
        }

        def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find{ f -> new File(f, "android.jar").exists()}

        if(!platformRoot) {
            logger.warn("No android.jar found, skipping Android build")
            return
        }

        // Находим d8
        def buildToolsDir = new File("$sdkRoot/build-tools")
        def buildTools = buildToolsDir.listFiles()?.find{ it.isDirectory() }
        
        if(!buildTools) {
            logger.warn("No build-tools found, skipping Android build")
            return
        }
        
        def d8 = new File(buildTools, isWindows ? "d8.bat" : "d8")
        if(!d8.exists()) {
            logger.warn("d8 not found at ${d8.path}, skipping Android build")
            return
        }

        //collect dependencies needed for desugaring
        def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect{ "--classpath $it.path" }.join(" ")

        //dex and desugar files - this requires d8 in your PATH
        def desktopJar = new File("$buildDir/libs/${project.name}Desktop.jar")
        def androidJar = new File("$buildDir/libs/${project.name}Android.jar")
        
        def command = "$d8.path $dependencies --min-api 14 --output $androidJar.path $desktopJar.path"
        logger.lifecycle("Running: $command")
        
        def process = command.execute(null, new File("$buildDir/libs"))
        process.waitForProcessOutput(System.out, System.err)
        
        if(process.exitValue() != 0) {
            throw new GradleException("d8 failed with exit code ${process.exitValue()}")
        }
        
        if(!androidJar.exists()) {
            throw new GradleException("Android JAR was not created at ${androidJar.path}")
        }
    }
}

jar{
    archiveFileName = "${project.name}Desktop.jar"

    from{
        configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }
    }

    from(projectDir){
        include "mod.hjson"
    }

    from("lib/assets/"){
        include "**"
    }
    
    from(projectDir){
        include "icon.png"
    }
}

task deploy(type: Jar){
    dependsOn jarAndroid
    dependsOn jar
    archiveFileName = "${project.name}.jar"

    from{ [zipTree("$buildDir/libs/${project.name}Desktop.jar"), zipTree("$buildDir/libs/${project.name}Android.jar")] }

    doLast{
        delete{
            delete "$buildDir/libs/${project.name}Desktop.jar"
            delete "$buildDir/libs/${project.name}Android.jar"
        }
    }
}
